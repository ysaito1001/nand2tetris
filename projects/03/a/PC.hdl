// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=outT, out=outTPlusOne);
    Mux16(a=outT, b=outTPlusOne, sel=inc, out=maybeInc);

    Mux16(a=maybeInc, b=in, sel=load, out=loadElseMaybeInc);

    Mux16(a=loadElseMaybeInc, b=false, sel=reset, out=resetElseLoadElseMaybeInc);

    Register(in=resetElseLoadElseMaybeInc, load=true, out=outT, out=out);
}
